use std::str::FromStr;
use metric::{MetricKind, Metric};
use std::rc::Rc;
use string_cache::Atom;

grammar;

Kind: MetricKind = {
    "g" => MetricKind::Gauge,
    "ms" => MetricKind::Timer,
    "h" => MetricKind::Histogram,
    "c" => MetricKind::Counter(1.0),
    "c@" <Num> => MetricKind::Counter(<>),
};

MetricName: Atom = <s:r"[A-Za-z][_A-Z0-9a-z+/=\.-]*"> => Atom::from(s);

Num: f64 = <s:r"[-+]?[0-9]+\.?[0-9]*"> => f64::from_str(s).unwrap();

MetricLine: Rc<Metric> = {
    <name:MetricName> ":" <val:Num> "|" <k:Kind> => Rc::new(Metric::new(name, val, k))
};

pub MetricPayload: Vec<Rc<Metric>> = { (<MetricLine>)+ };
