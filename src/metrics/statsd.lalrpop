use std::str::FromStr;
use metric::{MetricKind, MetricSign, Metric};
use string_cache::Atom;

grammar;

Kind: MetricKind = {
    "g" => MetricKind::Gauge,
    "g@" <Num> => MetricKind::Gauge,
    "ms" => MetricKind::Timer,
    "h" => MetricKind::Histogram,
    "c" => MetricKind::Counter(1.0),
    "c@" <Num> => MetricKind::Counter(<>),
};

MetricName: Atom = <s:r"[A-Za-z][_A-Z0-9a-z+/=\.-]*"> => Atom::from(s);

Sign: Option<MetricSign> = {
    "+" => Some(MetricSign::Positive),
    "-" => Some(MetricSign::Negative),
};

Num: f64 = <s:r"[0-9]+\.?[0-9]*"> => f64::from_str(s).unwrap();

MetricLine: Metric = {
    <name:MetricName> ":" <sign: Sign> <val:Num> "|" <k:Kind> => Metric::new(name, val, k, sign),
    <name:MetricName> ":" <val:Num> "|" <k:Kind> => Metric::new(name, val, k, None)
};

pub MetricPayload: Vec<Metric> = { (<MetricLine>)+ };
